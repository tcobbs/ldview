# Maintainer: Peter Bartfai <pbartfai[at]stardust[dot]hu>
pkgname=('ldview' 'ldview-qt5' 'ldview-osmesa')
pkgver=4.6.1
#pkgver=4.4.1beta2
pkgrel=1
pkgdesc="A real-time 3D viewer for displaying LDraw models"
url="http://github.com/tcobbs/ldview"
arch=('x86_64' 'i686')
license=('GPL')
depends=('qt6-base' 'libpng' 'libjpeg-turbo' 'gl2ps' 'tinyxml' 'minizip')
depends_qt5=('qt5-base' 'libpng' 'libjpeg-turbo' 'gl2ps' 'tinyxml' 'minizip')
depends_osmesa=('mesa-amber' 'libglvnd' 'libpng' 'libjpeg-turbo' 'gl2ps' 'tinyxml' 'minizip')
makedepends=('qt6-tools' 'qt5-tools' 'glu' 'gcc' 'make' 'cmake' 'extra-cmake-modules' 'kio5')
conflicts=()
replaces=()
backup=()
source=("ldview-git.tar.gz")
md5sums=(SKIP)

build() {
  test -d ${srcdir}/ldview-git/QT && cd ${srcdir}/ldview-git/QT || cd ..
  lrelease-qt5 LDView.pro
  qmake6
  make ${MAKEFLAGS} clean
  make ${MAKEFLAGS} all
  cp -f LDView LDView.qt6
  qmake-qt5
  make ${MAKEFLAGS} clean
  make ${MAKEFLAGS} all
  cp -f LDView LDView.qt5
  cd kde5
  if [ -d build ]; then rm -rf build ; fi
  mkdir -p build
  cd build
  if cmake -DCMAKE_INSTALL_PREFIX=/usr .. ; then
    make ${MAKEFLAGS}
  fi
  cd ../../../OSMesa
  make ${MAKEFLAGS} all
}
 
package_ldview() {
  test -d ${srcdir}/ldview-git/QT && cd ${srcdir}/ldview-git/QT || cd ..
  make INSTALL_ROOT="${pkgdir}" install
  cp -f LDView.qt6 ${pkgdir}/usr/bin/LDView
  cd kde5/build
  make DESTDIR="${pkgdir}" install
}

package_ldview-qt5() {
  test -d ${srcdir}/ldview-git/QT && cd ${srcdir}/ldview-git/QT || cd ..
  make INSTALL_ROOT="${pkgdir}" install
  cp -f LDView.qt5 ${pkgdir}/usr/bin/LDView
  cd kde5/build
  make DESTDIR="${pkgdir}" install
}

package_ldview-osmesa() {
  test -d ${srcdir}/ldview-git/OSMesa && cd ${srcdir}/ldview-git/OSMesa || cd ../../OSMesa
  make PREFIX="${pkgdir}" install
}
